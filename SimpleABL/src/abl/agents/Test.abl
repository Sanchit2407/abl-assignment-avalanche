package abl.generated;
// the package corresponds to where generated Java files are placed.

import abl.wmes.*;
import abl.actions.*;
import abl.sensors.*;

behaving_entity Test {
	
	register act fire(int, int, int, int) with Fire;
	
	register wme BotWME with BotSensor;
	
	wme AmmoWME {
		int count;
		int botId;
	}
	
	int MAX_AMMO = 5;
	
	sequential behavior fireABullet() {
		precondition {
			(BotWME id::id locationX::botX locationY::botY)
			ammoWME = (AmmoWME botId==id count>0 count::count)
		}
		act fire(botX,botY,1,1);
		mental_act {
			ammoWME.setCount(count - 1); 
		}
		subgoal Wait(500);
	}
	
	sequential behavior Wait(int milliseconds) {
		long finish;
	    mental_act { 
	    	finish = System.currentTimeMillis() + milliseconds; 
	    }
	    
	    with ( success_test { (System.currentTimeMillis() > finish) } ) wait;
	}
	
	sequential behavior initializeBots() {
		precondition {
			(BotWME id::id)
			!(AmmoWME botId==id)
		}
		specificity 2;
		mental_act{
			AmmoWME ammoWME = new AmmoWME();
			ammoWME.setCount(MAX_AMMO);
			ammoWME.setBotId(id);
			BehavingEntity.getBehavingEntity().addWME(ammoWME);
		}
		subgoal initializeBots();
	}
	
	sequential behavior initializeBots() {
		precondition {
			(BotWME id::id)
			!(AmmoWME botId!=id)
		}
		specificity 1;
		succeed_step;
	}
	
	initial_tree {
		with (priority 11) mental_act {
			System.out.println("The Test agent has started on initial_tree.");
		}
		
		with (priority 10, persistent when_fails) subgoal initializeBots();
		
		with (priority 1, persistent) subgoal fireABullet();
		
	}
}